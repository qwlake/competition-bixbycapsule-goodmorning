// 결과 화면에 대한 보여주는 방식을 설정 (https://bixbydevelopers.com/dev/docs/reference/type/result-view)
result-view {
  // 결과 화면을 보여주고 싶은 output concept을 지정
  match: StoreInfo(this)
  
  message {
    // 결과 화면에 보여줄 dialog를 설정
    if(!exists(this)){
      template-macro (no_result)
    }else{
      template-macro (result_answer) 
    }
    
  }
  
  render {
    // 어떻게 화면을 그릴 것인가에 대하여 설정
    // 결과 값 갯수에 따라 다르게 보여줌
    if (size(this) == 1) {
      layout-macro (storeInfo_details) {
        param (info) { // StoreInfo_Details_Macro.layout.bxb의 param명을 기입
          expression (this) // 결과 객체를 layout으로 전달
        }
      }    
    }else {
      list-of (this) {
        where-each (info) {
          layout-macro (storeInfo_summary) {
            param (info) { // StoreInfo_Summary_Macro.layout.bxb의 param명을 기입
              expression (info) // 결과 객체들중 하나를 layout으로 전달
            }
          }
        }
      }
    }
  }
  
  // 결과 화면 하단에 다음에 이어질 수 있는 발화를 보여줌(https://bixbydevelopers.com/dev/docs/dev-guide/developers/enhancing-UX.conversation-drivers)
  // continuation 발화를 하시려면 training에서 continiation-of를 사용해야함
  // 참조 (https://bixbydevelopers.com/dev/docs/dev-guide/developers/training.intro-training#add-training-examples-for-continuations)
  conversation-drivers {
    if (size(this) == 1) {
      conversation-driver {
        template ("고양시는?")
      }
    }else{
      conversation-driver {
        template ("인계동은 어때?")
      }
    }
  }
}